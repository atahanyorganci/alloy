WHITESPACE = _{ "\t" | " " | "\n"}

digit = { ASCII_DIGIT | "_" }
digits = _{ ASCII_DIGIT ~ digit* }
plus = { "+" }
minus = { "-" }
sign = _{ plus | minus }

binary = @{ "0b" ~ (ASCII_BIN_DIGIT | "_")+ }
octal = @{ "0o" ~ (ASCII_OCT_DIGIT | "_")+ }
hexadecimal = @{ "0x" ~ (ASCII_HEX_DIGIT | "_")+ }
decimal = @{ !("0b" | "0o" | "0x") ~ digits ~ !(".") }
unsigned = _{ binary | octal | hexadecimal | decimal}
integer = { sign? ~ unsigned }

dot_float = @{ digits? ~ "." ~ digits }
float_dot = @{ digits ~ "." ~ digits? }
float = { sign? ~ (float_dot | dot_float) }

boolean = { "true" | "false" }
value = { float | integer | boolean }

add = { "+" }
subtract = { "-" }
multiply = { "*" }
divide = { "/" }
power = { "**" }
less_than = { "<"}
less_than_eq = { "<="}
greater_than = { ">"}
greater_than_eq = { ">="}
equal_to = { "==" }
not_equal_to = { "!=" }
logical_and = { "and" | "&&" }
logical_or = { "or" | "||" }
logical_xor = { "xor" }
binary_op = _{ add
    | subtract
    | multiply
    | divide
    | power
    | less_than
    | less_than_eq
    | greater_than
    | greater_than_eq
    | equal_to
    | not_equal_to
    | logical_and
    | logical_or
    | logical_xor
}

// Keywords
k_if = @{ "if" ~ WHITESPACE }
k_else = @{ "else" ~ WHITESPACE }
k_print = @{ "print" ~ WHITESPACE }
k_while = @{ "while" ~ WHITESPACE }
k_for = @{ "for" ~ WHITESPACE }
k_return = @{ "return" ~ WHITESPACE }
k_var = @{ "var" ~ WHITESPACE }
k_const = @{ "const" ~ WHITESPACE }
k_continue = @{ "continue" }
k_break = @{ "break" }
k_in = @{ "in" ~ WHITESPACE }
keyword = { k_if
    | k_else
    | k_print
    | k_while
    | k_for
    | k_return
    | k_var
    | k_const
    | k_continue
    | k_break
    | k_in
}

term = _{ value | "(" ~ expression ~ ")" }
expression = { term ~ (binary_op ~ term)* }

identifier = @{ !keyword ~ ASCII_ALPHA ~ ASCII_ALPHANUMERIC* }

// Statements
semi = _{ ";" }

// Print Statement
print_statement = { k_print ~ expression ~ semi}

// Declaration and Assignment Statements
declaration_statement = { k_var ~ identifier ~ semi
    | (k_const | k_var) ~ identifier ~ "=" ~ expression ~ semi
}
assignment_statement = { identifier ~ "=" ~ expression ~ semi }

// Block Statement
scoped_statements = _{ "{" ~ statements ~ "}" }
block_statement = { scoped_statements }

// If Statement
if_body = { k_if ~ expression ~ scoped_statements }
else_if_body = { k_else ~ k_if ~ expression ~ scoped_statements }
else_body = { k_else ~ scoped_statements }
if_statement = { if_body ~ else_if_body* ~ else_body? }

// Loop Statements
break_statement = { k_break ~ semi }
continue_statement = { k_continue ~ semi }
loop_body_statement = _{ statement | break_statement | continue_statement }
loop_body = { "{" ~ loop_body_statement* ~ "}"}
while_statement = { k_while ~ expression ~ loop_body }
for_statement = { k_for ~ identifier ~ k_in ~ expression ~ loop_body }

expression_statement = { expression ~ semi }

statement = _{ print_statement
    | if_statement
    | declaration_statement
    | assignment_statement
    | while_statement
    | for_statement
    | block_statement
    | expression_statement
}
statements = { statement* }
program = _{ SOI ~ statement* ~ EOI }
